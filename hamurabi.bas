1 LetA=100,B=5,C=0,D=2800,E=200,F=3,G=3000
2 LetH=1000,J=1
3 Prt"HAMURABI,",C,"STARVED",B
4 Prt"PEOPLE CAME"
5 IfJ>0ThenGoto8
6 LetL=A/2,A=Int[L]
7 Prt"PLAGUE","KILLED HALF"
8 Prt"POPULATION",A 
9 Gos65
10 Prt"HARVESTED",G,"BUSHEL/ACRE",F,"RATS ATE"
11 PrtE,"BUSHELS",D
12 Gos65
13 LetK=Rnd[5],K=K+17
14 Prt"CITY OWNS",H,"ACRES","LAND WORTH",K
15 Prt"BUSHEL/ACRE"
16 Gos65
17 Inp"BUY HOW MANYACRES",I
18 Gos59
19 IfI=0ThenGoto25
20 LetJ=I*K
21 IfJ<=DThenGoto24
22 Gos62
23 Goto17
24 LetD=D-J,H=H+I
25 Inp"SELL HOW    MANY ACRES",I
26 Gos59
27 IfI=0ThenGoto33
28 IfI<HThenGoto32
29 IfI=HThenGoto67
30 Gos62
31 Goto25
32 LetH=H-I,D=D+K*I
33 Prt"HOW MANY    BUSHELS TO  DISTRIBUTE"
34 Inp"AS FOOD",I
35 Gos59
36 IfI<=DThenGoto39
37 Gos62
38 Goto33
39 LetD=D-I,I=I/20,I=Int[I],C=A-I,B=0
40 IfC>=0ThenGoto42
41 LetB=C/2,C=0
42 Inp"HOW MANY    ACRES SHALL WE PLANT",I
43 Gos59
44 IfI>HThenGoto47
45 LetJ=I/2,J=Int[J]
46 IfJ<=DThenGoto49
47 Gos62
48 Goto42
49 IfI>10*AThenGoto47
50 LetD=D-J,F=Rnd[4],F=F+1,G=F*I
51 LetJ=Rnd[6],J=J/100,I=J,J=Rnd[99],J=J/10000
52 LetI=I+J,J=Rnd[9],J=J/100000,I=I+J,E=[D+G]*I
53 LetE=Int[E],D=D-E+G,J=Rnd[9],J=J+1
54 LetB=B+[5-F]*D/600+1,B=Int[B]
55 IfB<=50ThenGoto57
56 LetB=50
57 LetA=A+B-C,J=Rnd[9]
58 Goto3
59 IfI<0ThenLetI=-I
60 LetI=Int[I]
61 Rtn
62 Prt"HAMURABI,","THINK AGAIN","YOU HAVE",A
63 Prt"PEOPLE",H,"ACRES AND",D,"BUSHELS"
64 Rtn
65 Inp"1=CONTINUE",L
66 Rtn
67 Prt"SOLD ALL","YOUR LAND."
